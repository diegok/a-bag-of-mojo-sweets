<!doctype html>
<html lang="en">
  <head>
    <meta charset="utf-8">
    <title>A bot to rule them all</title>
    <meta name="description" content="">
    <meta name="author" content="Diego Kuperman">
    <meta name="apple-mobile-web-app-capable" content="yes" />
    <meta name="apple-mobile-web-app-status-bar-style" content="black-translucent" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no">
    <link rel="stylesheet" href="css/reveal.min.css">
    <link rel="stylesheet" href="css/theme/solarized.css" id="theme">
    <!-- For syntax highlighting -->
    <link rel="stylesheet" href="lib/css/zenburn.css">
    <!-- If the query includes 'print-pdf', include the PDF print sheet -->
    <script>
    if( window.location.search.match( /print-pdf/gi ) ) {
      var link = document.createElement( 'link' );
      link.rel = 'stylesheet';
      link.type = 'text/css';
      link.href = 'css/print/pdf.css';
      document.getElementsByTagName( 'head' )[0].appendChild( link );
    }
    </script>
    <!--[if lt IE 9]>
    <script src="lib/js/html5shiv.js"></script>
    <![endif]-->
  </head>
  <body>
    <div class="reveal">
      <!-- Any section element inside of this container is displayed as a slide -->
      <div class="slides">

	<section>
	  <h1>A bag of mojo sweets</h1>
	  <br><br><br><br><br>
	  <h3>Diego Kuperman | @freekey</h3>
    <a href="https://diegok.github.io/a-bag-of-mojo-sweets/">https://diegok.github.io/a-bag-of-mojo-sweets/</a>
	</section>

	<section>
	  <h1>Hola</h1>
	</section>

	<section>
	  <h1>I ❤️ mojo!</h1>
	</section>
	<section>
	  <img src="img/papas-arrugadas-mojo-picon.jpg" alt="" class="plain">
	</section>

	<section>
	  <h1>Mojolicious</h1>
	  <h3>Real time web framework</h3>
	</section>

	<section>
	  <pre><code data-noescape class="perl">

 use Mojolicious::Lite;

 get '/' => {text => 'I ♥ Mojolicious!'};

 app->start;

    </code></pre>
	</section>

	<section>
	  <h1>I ❤️ mojo!</h1>
	</section>
	<section>
	  <h1>Mojo</h1>
	  <h3>Web development toolkit</h3>
	</section>

	<section>
	  <img src="img/mojo-sweets.jpg" alt="" class="plain">
	</section>

	<section>
	  <h1>Big distribution</h1>
	  <h3>No deps besides perl 5.24</h3>
    <h3>(works with perl >= 5.10.1)</h3>
	</section>

	<section>
      <pre><code>
> mojo get https://metacpan.org/release/Mojolicious '.release-modules li' all \
     | grep -P '\S+'
      </code></pre>
	</section>

	<section>
      <pre style="margin-top:-60px;"><code data-trim>
Mojo - Web development toolkit
Mojo::Asset - HTTP content storage base class
Mojo::Asset::File - File storage for HTTP content
Mojo::Asset::Memory - In-memory storage for HTTP content
Mojo::Base - Minimal base class for Mojo projects
Mojo::ByteStream - ByteStream
Mojo::Cache - Naive in-memory cache
Mojo::Collection - Collection
Mojo::Content - HTTP content base class
Mojo::Content::MultiPart - HTTP multipart content
Mojo::Content::Single - HTTP content
Mojo::Cookie - HTTP cookie base class
Mojo::Cookie::Request - HTTP request cookie
Mojo::Cookie::Response - HTTP response cookie
Mojo::DOM - Minimalistic HTML/XML DOM parser with CSS selectors
Mojo::DOM::CSS - CSS selector engine
Mojo::DOM::HTML - HTML/XML engine
Mojo::Date - HTTP date
Mojo::EventEmitter - Event emitter base class
Mojo::Exception - Exceptions with context
Mojo::File - File system paths
Mojo::Headers - HTTP headers
Mojo::HelloWorld - Hello World!
Mojo::Home - Home sweet home
Mojo::IOLoop - Minimalistic event loop
Mojo::IOLoop::Client - Non-blocking TCP/IP and UNIX domain socket client
Mojo::IOLoop::Delay - Manage callbacks and control the flow of events
Mojo::IOLoop::Server - Non-blocking TCP and UNIX domain socket server
Mojo::IOLoop::Stream - Non-blocking I/O stream
Mojo::IOLoop::Subprocess - Subprocesses
Mojo::IOLoop::TLS - Non-blocking TLS handshake
Mojo::JSON - Minimalistic JSON
Mojo::JSON::Pointer - JSON Pointers
Mojo::Loader - Load all kinds of things
Mojo::Log - Simple logger
      </code></pre>
	</section>
	<section>
      <pre style="margin-top:-60px;"><code data-trim>
Mojo::Message - HTTP message base class
Mojo::Message::Request - HTTP request
Mojo::Message::Response - HTTP response
Mojo::Parameters - Parameters
Mojo::Path - Path
Mojo::Reactor - Low-level event reactor base class
Mojo::Reactor::EV - Low-level event reactor with libev support
Mojo::Reactor::Poll - Low-level event reactor with poll support
Mojo::Server - HTTP/WebSocket server base class
Mojo::Server::CGI - CGI server
Mojo::Server::Daemon - Non-blocking I/O HTTP and WebSocket server
Mojo::Server::Hypnotoad - A production web serv...ALL GLORY TO THE HYPNOTOAD!
Mojo::Server::Morbo - Tonight at 11...DOOOOOOOOOOOOOOOM!
Mojo::Server::Morbo::Backend - Morbo backend base class
Mojo::Server::Morbo::Backend::Poll - Morbo default backend
Mojo::Server::PSGI - PSGI server
Mojo::Server::Prefork - Pre-forking non-blocking I/O HTTP and WebSocket server
Mojo::Template - Perl-ish templates
Mojo::Transaction - Transaction base class
Mojo::Transaction::HTTP - HTTP transaction
Mojo::Transaction::WebSocket - WebSocket transaction
Mojo::URL - Uniform Resource Locator
Mojo::Upload - Upload
Mojo::UserAgent - Non-blocking I/O HTTP and WebSocket user agent
Mojo::UserAgent::CookieJar - Cookie jar for HTTP user agents
Mojo::UserAgent::Proxy - User agent proxy manager
Mojo::UserAgent::Server - Application server
Mojo::UserAgent::Transactor - User agent transactor
Mojo::Util - Portable utility functions
Mojo::WebSocket - The WebSocket protocol
Mojolicious - Real-time web framework
Mojolicious::Command - Command base class
Mojolicious::Command::cgi - CGI command
Mojolicious::Command::cpanify - CPAN-ify command
Mojolicious::Command::daemon - Daemon command
      </code></pre>
	</section>
	<section>
      <pre style="margin-top:-60px;"><code data-trim>
Mojolicious::Command::eval - Eval command
Mojolicious::Command::generate - Generator command
Mojolicious::Command::generate::app - App generator command
Mojolicious::Command::generate::lite_app - Lite app generator command
Mojolicious::Command::generate::makefile - Makefile generator command
Mojolicious::Command::generate::plugin - Plugin generator command
Mojolicious::Command::get - Get command
Mojolicious::Command::inflate - Inflate command
Mojolicious::Command::prefork - Pre-fork command
Mojolicious::Command::psgi - PSGI command
Mojolicious::Command::routes - Routes command
Mojolicious::Command::test - Test command
Mojolicious::Command::version - Version command
Mojolicious::Commands - Command line interface
Mojolicious::Controller - Controller base class
Mojolicious::Lite - Micro real-time web framework
Mojolicious::Plugin - Plugin base class
Mojolicious::Plugin::Config - Perl-ish configuration plugin
Mojolicious::Plugin::DefaultHelpers - Default helpers plugin
Mojolicious::Plugin::EPLRenderer - Embedded Perl Lite renderer plugin
Mojolicious::Plugin::EPRenderer - Embedded Perl renderer plugin
Mojolicious::Plugin::HeaderCondition - Header condition plugin
Mojolicious::Plugin::JSONConfig - JSON configuration plugin
Mojolicious::Plugin::Mount - Application mount plugin
Mojolicious::Plugin::PODRenderer - POD renderer plugin
Mojolicious::Plugin::TagHelpers - Tag helpers plugin
Mojolicious::Plugins - Plugin manager
Mojolicious::Renderer - Generate dynamic content
Mojolicious::Routes - Always find your destination with routes
Mojolicious::Routes::Match - Find routes
Mojolicious::Routes::Pattern - Route pattern
Mojolicious::Routes::Route - Route
Mojolicious::Sessions - Session manager based on signed cookies
Mojolicious::Static - Serve static files
Mojolicious::Types - MIME types
Mojolicious::Validator - Validate values
Mojolicious::Validator::Validation - Perform validations
Test::Mojo - Testing Mojo
ojo - Fun one-liners with Mojo
      </code></pre>
	</section>

	<section>
	  <h1>109 libs</h1>
    <pre><code>
> mojo get https://metacpan.org/release/Mojolicious '.release-modules li' all
     | grep -P '\S+' \
     | wc -l
    </code></pre>
	</section>

	<section>
	  <h1>Mojolicious</h1>
	  <h1>-</h1>
	  <h1>WEB</h1>
	</section>

	<section>
	  <h1>43 libs</h1>
    <pre><code>
> mojo get https://metacpan.org/release/Mojolicious '.release-modules li' all \
     | grep -P '\S+' \
     | grep -Piv 'http|web|mojolicious' \
     | wc -l
    </code></pre>
	</section>

	<section>
	  <h1>25 libs</h1>
    <pre><code>
> mojo get https://metacpan.org/release/Mojolicious '.release-modules li' all \
     | grep -P '\S+' \
     | grep -Piv 'http|web|mojolicious'
     | grep -Piv 'server|useragent|url|upload|test|dom|transaction'
     | wc -l
    </code></pre>
	</section>

	<section>
	  <h1>21 libs</h1>
    <pre><code>
> mojo get https://metacpan.org/release/Mojolicious '.release-modules li' all \
    | grep -P '\S+' \
    | grep -Piv 'http|web|mojolicious'
    | grep -Piv 'server|useragent|url|upload|test|dom|transaction'
    | grep -Piv 'path|parameters|helloworld|^\s*ojo'
    | wc -l
    </code></pre>
	</section>

	<section>
    <pre><code>
    Mojo::Base - Minimal base class for Mojo projects
    Mojo::Cache - Naive in-memory cache
    Mojo::Collection - Collection
    Mojo::EventEmitter - Event emitter base class
    Mojo::Exception - Exceptions with context
    Mojo::File - File system paths
    Mojo::Home - Home sweet home
    Mojo::IOLoop - Minimalistic event loop
    Mojo::IOLoop::Delay - Promises/A+ and flow-control helpers
    Mojo::IOLoop::Stream - Non-blocking I/O stream
    Mojo::IOLoop::Subprocess - Subprocesses
    Mojo::IOLoop::TLS - Non-blocking TLS handshake
    Mojo::JSON - Minimalistic JSON
    Mojo::JSON::Pointer - JSON Pointers
    Mojo::Loader - Load all kinds of things
    Mojo::Log - Simple logger
    Mojo::Reactor - Low-level event reactor base class
    Mojo::Reactor::EV - Low-level event reactor with libev support
    Mojo::Reactor::Poll - Low-level event reactor with poll support
    Mojo::Template - Perl-ish templates
    Mojo::Util - Portable utility functions
    </code></pre>
	</section>

	<section>
	  <h1>Mojo::Base</h1>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-trim data-noescape class="perl">
package Cat;
use Mojo::Base -base;

has name => 'Nyan';
has ['age', 'weight'] => 4;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 package Tiger;
 use Mojo::Base 'Cat';

 has friend  => sub { Cat->new };
 has stripes => 42;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;

 my $mew = Cat->new(name => 'Longcat');
 say $mew->age;
 say $mew->age(3)->weight(5)->age;

 my $rawr = Tiger->new(stripes => 38, weight => 250);
 say $rawr->tap(sub { $_->friend->name('Tacgnol') })->weight;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 # use Mojo::Base -strict;
 use strict;
 use warnings;
 use utf8;
 use feature ':5.10';
 use IO::Handle ();

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 # use Mojo::Base -base;
 use strict;
 use warnings;
 use utf8;
 use feature ':5.10';
 use IO::Handle ();
 push @ISA, 'Mojo::Base';
 sub has { Mojo::Base::attr(__PACKAGE__, @_) }

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 # use Mojo::Base 'SomeBaseClass';
 use strict;
 use warnings;
 use utf8;
 use feature ':5.10';
 use IO::Handle ();
 require SomeBaseClass;
 push @ISA, 'SomeBaseClass';
 sub has { Mojo::Base::attr(__PACKAGE__, @_) }

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Base</h3>
	  <pre><code data-noescape class="perl">
 my $new_class = SubClass->with_roles('SubClass::Role::One');
 my $new_class = SubClass->with_roles('+One', '+Two');
 $object       = $object->with_roles('+One', '+Two');

 # Create a new class with the role "SubClass::Role::Foo" and instantiate it
 my $new_class = SubClass->with_roles('+Foo');
 my $object    = $new_class->new;

    </code></pre>
    <p>Role support depends on Role::Tiny (2.000001+)</p>
	</section>

	<section>
	  <h1>Mojo::EventEmitter</h1>
	</section>
	<section>
    <h3>Mojo::EventEmitter</h3>
	  <pre><code data-noescape class="perl">
 package Cat;
 use Mojo::Base 'Mojo::EventEmitter';

 # Emit events
 sub poke {
   my $self = shift;
   $self->emit(roar => 3);
 }

 package main;

 # Subscribe to events
 my $tiger = Cat->new;
 $tiger->on(roar => sub {
   my ($tiger, $times) = @_;
   say 'RAWR!' for 1 .. $times;
 });
 $tiger->poke;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::EventEmitter</h3>
	  <pre><code data-noescape class="perl">
 $e->on(error => sub {
   my ($e, $err) = @_;
   say "This looks bad: $err";
 });

 $e = $e->catch(sub {...}); # Same thing, shorter.
     </code></pre>
     <p>Special event, fatal in not handled.</p>
	</section>
	<section>
    <h3>Mojo::EventEmitter</h3>
	  <pre><code data-noescape class="perl">
 my $cb = $e->once(foo => sub {...});

 # Unsubscribe last subscriber
 $e->unsubscribe(foo => $e->subscribers('foo')->[-1]);

 # Change order of subscribers
 @{$e->subscribers('foo')} = reverse @{$e->subscribers('foo')};
    </code></pre>
	</section>


	<section>
	  <h1>Mojo::Exception</h1>
    <h3>Exceptions with context</h3>
	</section>
	<section>
    <h3>Mojo::Exception</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;;
 use Mojo::Exception;

 # Throw exception and show stack trace
 eval { Mojo::Exception->throw('Something went wrong!') };
 say "$_->[0]: $_->[1]($_->[2])" for @{$@->frames};
 say $@->line->[1]; # eval { ...
 say $@; # Something whent wrong!

    </code></pre>
	</section>

	<section>
	  <img src="img/exception.png" alt="" class="plain">
	</section>

	<section>
	  <h1>Mojo::Log</h1>
    <h3>Simple logger</h3>
	</section>
	<section>
    <h3>Mojo::Log</h3>
	  <pre><code data-trim data-noescape class="perl">
use Mojo::Log;

# Log to STDERR
my $log = Mojo::Log->new;

# Customize log file location and minimum log level
my $log = Mojo::Log->new(path => '/var/log/mojo.log', level => 'warn');

my $level = $log->level;
$log      = $log->level('debug');
# also $ENV{MOJO_LOG_LEVEL}

# Log messages
$log->debug('Not sure what is happening here');
$log->info('FYI: it happened again');
$log->warn('This might be a problem');
$log->error('Garden variety error');
$log->fatal('Boom');
    </code></pre>
	</section>
	<section>
    <h3>Mojo::Log</h3>
	  <pre><code data-trim data-noescape class="perl">
my $cb = $log->format;
$log->format(sub {
    my ($time, $level, @lines) = @_;
    return "[$level] I ♥ Mojolicious and don't care the message\n";
});

$log->on(message => sub {
    my ($log, $level, @lines) = @_;

    # Emitted for every log message
});
    </code></pre>
	</section>
	<section>
    <h3>Mojo::Log</h3>
	  <pre><code data-trim data-noescape class="perl">
my $handle = $log->handle;
$log       = $log->handle(IO::Handle->new);

$log->append("[Sat Nov 04 16:45:04 2017] [info] I ♥ Barcelona.pm\n");

my $path = $log->path
$log     = $log->path('/var/log/mojo.log');
    </code></pre>
	</section>
	<section>
    <h3>Mojo::Log</h3>
	  <pre><code data-trim data-noescape class="perl">
my $history = $log->history;
$log        = $log->history([[time, 'debug', 'That went wrong']]);

my $size = $log->max_history_size; # 10 by default
$log     = $log->max_history_size(5);
    </code></pre>
	</section>


	<section>
	  <h1>Mojo::Loader</h1>
    <h3>Load all kinds of things</h3>
	</section>
	<section>
    <h3>Mojo::Loader</h3>
	  <pre><code data-trim data-noescape class="perl">
use Mojo::Base -strict;
use Mojo::Loader qw(data_section find_modules load_class);

# Find modules in a namespace
for my $module (find_modules 'Some::Namespace') {

  # Load them safely
  if ( my $e = load_class $module ) {
    warn ref $e ? qq{Loading "$module" failed: $e}
                : qq{"$module" not found!};
    next;
  }

  # And extract files from the DATA section
  say data_section($module, 'some_file.txt');
}
    </code></pre>
	</section>
	<section>
    <h3>Mojo::Loader</h3>
	  <pre><code data-trim data-noescape class="perl">
package PerlMongers::Barcelona;
use Mojo::Base -base;

has cpan_authors => sub {{ map { chomp; split /\s*=\s*/ } <DATA> }};

__DATA__
ALEXM    = Alex Muntada
DIEGOK   = Diego Kuperman
ENELL    = Enrique Nell
FXN      = Xavier Noria
JAVIER   = Javier Arturo Rodríguez Gutiérrez
JLMARTIN = José Luis Martínez Torres
MRUIZ    = Míquel Ruiz Martín
    </code></pre>
	</section>
	<section>
    <h3>Mojo::Loader</h3>
	  <pre><code data-noescape class="perl">
 package PerlMongers::Barcelona;
 use Mojo::Base -base;
 use Mojo::Loader qw/ data_section /;

 has cpan_authors => sub {{ map { split /\s*=\s*/ } _lines('authors') }};
 has events       => sub {[ _lines('events') ]};

 sub _lines { split /\n/, data_section(__PACKAGE__ => $_[0]) }

 __DATA__

 @@ authors
 ALEXM    = Alex Muntada
 DIEGOK   = Diego Kuperman
 ENELL    = Enrique Nell
 FXN      = Xavier Noria

 @@ events
 Barcelona Perl & Friends 2017
 Barcelona Perl Workshop 2016
 Barcelona Perl Workshop 2015
 Barcelona Perl Workshop 2014

    </code></pre>
	</section>

	<section>
    <h3>Mojo::Loader</h3>
	  <pre><code data-noescape class="perl">
 package PerlMongers::Barcelona;
 use Mojo::Base -base;
 use Mojo::Loader qw/ data_section /;

 has cpan_authors => sub {{ map { split /\s*=\s*/ } _lines('authors') }};
 has events       => sub {[ _lines('events') ]};

 sub _lines { split /\n/, data_section(__PACKAGE__ => $_[0]) }

 __DATA__

 @@ authors (base64)
 QUxFWE0gPSBBbGV4IE11bnRhZGEKRElFR09LID0gRGllZ28gS3VwZXJtYW4KRU5FTEwgPSBFbnJp
 cXVlIE5lbGwKRlhOID0gWGF2aWVyIE5vcmlh

 @@ events (base64)
 QmFyY2Vsb25hIFBlcmwgJiBGcmllbmRzIDIwMTcKQmFyY2Vsb25hIFBlcmwgV29ya3Nob3AgMjAx
 NgpCYXJjZWxvbmEgUGVybCBXb3Jrc2hvcCAyMDE1CkJhcmNlbG9uYSBQZXJsIFdvcmtzaG9wIDIw
 MTQ=

    </code></pre>
	</section>

	<section>
	  <h1>Mojo::Template</h1>
    <h3>Perl-ish templates</h3>
	</section>
	<section>
    <h3>Mojo::Template</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::Template;

 # Use Perl modules
 my $mt = Mojo::Template->new;
 say $mt->render(<<'EOF');
 % use Time::Piece;

 % my $now = localtime;
 Time: <%= $now->hms %>
 EOF

 # Render with arguments
 say $mt->render(<<'EOF', [1 .. 13], 'Hello World!');
 % my ($numbers, $title) = @_;
 <%= $title %>
 % for my $i (@$numbers) {
   Test <%= $i %>
 % }
 EOF

 # Render with named variables
 say $mt->vars(1)->render(<<'EOF', {title => 'Hello World!'});
 {
   "title": "<%= $title %>"
 }
 EOF

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Template</h3>
	  <pre><code data-noescape class="perl">
 <% Perl code %>

 <%= Perl expression, replaced with result %>

 <%== Perl expression, replaced with XML escaped result %>

 <%# Comment, useful for debugging %>

 <%% Replaced with "<%", useful for generating templates %>

 % Perl code line, treated as "<% line =%>" (explained later)

 %= Perl expression line, treated as "<%= line %>"

 %== Perl expression line, treated as "<%== line %>"

 %# Comment line, useful for debugging

 %% Replaced with "%", useful for generating templates

    </code></pre>
	</section>

	<section>
	  <h1>Mojo::Collection</h1>
    <h3>Array based container for collections</h3>
	</section>
	<section>
    <h3>Mojo::Collection</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::Collection;

 # Manipulate collection
 my $collection = Mojo::Collection->new(qw(just works));
 unshift @$collection, 'it';
 say $collection->join(" "); # it just works

 # Chain methods
 $collection->map(sub { ucfirst })->shuffle->each(sub {
   my ($word, $num) = @_;
   say "$num: $word";
 });

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Collection</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::Collection qw(c);

 my $nums = c(1 .. 10);

 say $nums->grep(sub{ $_ % 2 })->size; # count odds
 say $nums->map(sub{ $_ % 2 || undef })->compact->size; # same

 say $nums->map(sub{[$_, 100 - $_]})->flatten->reduce(sub{ $a + $b });

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Collection</h3>
	  <pre><code data-noescape class="perl">
 c(1 .. 10)->map(sub{ ($_) x $_ })->reduce(sub{ $a + $b });

 c(1 .. 10)->map(sub{ $_ % 2 ? 'odd' : 'even' })->reduce(sub{ $a->{$b}++; $a }, {});

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Collection</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Collection qw(c);

 # "B P F"
 c('A', 'B', 'P', 'D', 'F')->slice(1, 2, 4)->join(' ');

 # "B P & F"
 c('F', 'B', 'P', 'D', '&')->slice(0, 4, 2, 1)->reverse->join(' ');

    </code></pre>
	</section>

	<section>
    <h3>Mojo::Collection</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Collection qw(c);

 c(5..15, 0..10)->uniq->sort->size; #

 c(5..15, 0..10)->map (sub{ Car->new(wheels => $_) })
               ->sort(sub{ $_->wheels })
               ->tap (sub{ $_->increment_wheels })
               ->map('wheels')->to_array;
               # [ 1..16 ]

    </code></pre>
	</section>

	<section>
	  <h1>Mojo::File</h1>
    <br>
	  <p>A scalar-based container for file system paths that provides a friendly API for dealing with different operating systems.</p>
	</section>
	<section>
    <h3>Mojo::File</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::File;

 my $path = Mojo::File->new('/home/diegok/.vimrc');

 say $path->slurp;     # ...
 say $path->dirname;   # /home/diegok
 say $path->basename;  # .vimrc
 say $path->sibling('.zshrc'); # /home/diegok/.zshrc

    </code></pre>
	</section>
	<section>
    <h3>Mojo::File</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::File 'path';

 say path('foo/bar')->make_path->child('test.txt')->spurt('Some content')->to_abs;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::File</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::File 'path';

 say path('/home/diegok/.vim')->list({dir=>1})->join("\n");

 say path('/home/diegok/.vim')->list_tree->grep(qr/README/)->join("\n");

    </code></pre>
	</section>
	<section>
    <h3>Mojo::File</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::File qw/ path tempdir /;

 my $tmp = tempdir;
 path('/home/diegok/.vim')->list_tree->grep(qr/README/)->each(sub{
    $_->copy_to($tmp)
 });

 say $tmp->list->join("\n");

    </code></pre>
	</section>
	<section>
    <h3>Mojo::File</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::File qw/ path /;

 my $handle    = $path->open('<:encoding(UTF-8)');
 my $handle_rw = $path->open('+<');

    </code></pre>
	</section>

	<section>
	  <h1>Mojo::Cache</h1>
    <h3>Naive in-memory cache with size limits.</h3>
	</section>
	<section>
    <h3>Mojo::Cache</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Cache;

 my $cache = Mojo::Cache->new(max_keys => 50);
 $cache->set(foo => 'bar');
 my $foo = $cache->get('foo');

    </code></pre>
	</section>

	<section>
	  <h1>In the bag...</h1>
	</section>
	<section>
	  <h1>There is another bag!</h1>
	</section>

	<section>
	  <h1>Mojo::Util</h1>
    <h3>Portable utility functions</h3>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-trim data-noescape class="perl">

 use Mojo::Util qw/ b64_encode /;
 use Mojo::File qw/ path /;

 my $b64 = b64_encode path('mojo-picon.png')->slurp;
 my $img = b64_decode $b64;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-noescape class="perl">

 package Foo;
 use Mojo::Base -base;
 use Mojo::Util qw/ deprecated extract_usage /;

 =head1 SYNOPSIS

   Usage: Foo->new->help

 =cut

 sub help     { extract_usage }
 sub old_help { deprecated 'old_help is DEPRECATED in favor of help' }

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-trim data-noescape class="perl">

my $checksum = md5_bytes $bytes;

# "acbd18db4cc2f85cedef654fccc4a4d8"
md5_sum 'foo';

my $checksum = sha1_bytes $bytes;

# "0beec7b5ea3f0fdbc95d0dd47f3c5bc275da8a33"
sha1_sum 'foo';

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-noescape class="perl">

 monkey_patch 'Foo',
   one   => sub { say 'One!' },
   two   => sub { say 'Two!' },
   three => sub { say 'Three!' };

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-noescape class="perl">
 # "Some \"word\" to test"
 my $quoted = quote trim '  Some "word" to test  ';

 say unquote $quoted;

 say unindent "  sub {\n    time\n  }\n";

    </code></pre>
	</section>
	<section>
    <h3>Mojo::Util</h3>
	  <pre><code data-noescape class="perl">

 use Mojo::Base -strict;
 use Mojo::Util qw/ md5_sum steady_time tablify /;
 use Mojo::File qw/ path /;

 my $t0 = steady_time;

 say tablify path('/usr/bin')->list->map(sub{[
   $_, eval{md5_sum($_->slurp)} || 'Unable to read file'
 ]});

 say 'Took: 'steady_time - $t0, 'secs';

    </code></pre>
	</section>

	<section>
	  <h1>Mojo::IOLoop</h1>
    <h3>Minimalistic event loop</h3>
	</section>
	<section>
    <h3>Mojo::IOLoop</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::IOLoop;

 Mojo::IOLoop->server({port => 3003} => sub {
   my ($loop, $stream) = @_;

   $stream->on(read => sub {
     my ($stream, $bytes) = @_;
     say "-> $bytes";
     $stream->write("ack: $bytes");
   });
 });

 Mojo::IOLoop->client({port => 3003} => sub {
   my ($loop, $err, $stream) = @_;

   $stream->on(read => sub { say '<- ', pop });

   my $count = 0;
   $loop->recurring(2 => sub { ++$count && $stream->write("ping($count)") })
 });

 Mojo::IOLoop->timer(10 => sub { shift->stop });

 Mojo::IOLoop->start unless Mojo::IOLoop->is_running;
    </code></pre>
	</section>

	<section>
    <h3>Mojo::IOLoop</h3>
	  <pre><code data-noescape class="perl">
 Mojo::IOLoop->subprocess(

   sub {
     my $subprocess = shift;
     sleep 5;
     return '♥', 'Mojolicious';
   },

   sub {
     my ($subprocess, $err, @results) = @_;
     say "Subprocess error: $err" and return if $err;
     say "I $results[0] $results[1]!";
   }

 );

    </code></pre>
	</section>

	<section>
    <h3>Mojo::IOLoop</h3>
	  <pre><code data-noescape class="perl">
 Mojo::IOLoop->delay(
   sub {
     my $delay = shift;
     Mojo::IOLoop->timer(2 => $delay->begin);
     say 'Second step in 2 seconds.';
   },

   sub {
     my $delay = shift;
     Mojo::IOLoop->timer(1 => $delay->begin);
     Mojo::IOLoop->timer(3 => $delay->begin);
     say 'Third step in 3 seconds.';
   },

   sub { say 'And done after 5 seconds total.' }
 )->wait;

    </code></pre>
	</section>

	<section>
    <h3>Mojo::IOLoop</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::IOLoop;

 my $promise = Mojo::IOLoop->delay;
 Mojo::IOLoop->timer(2 => sub{ $promise->resolve(2) });

 $promise->then(sub{
     my $waited = shift;

     my ($t1, $t3) = map { Mojo::IOLoop->delay } 1..2;
     Mojo::IOLoop->timer(1 => sub{ $t1->resolve(1 + $waited) });
     Mojo::IOLoop->timer(3 => sub{ $t3->resolve(3 + $waited) });

     $t1->all($t3);

 })->then(sub{ say "Done after $_[-1][0] seconds." })->wait;

    </code></pre>
	</section>
	<section>
    <h3>Mojo::IOLoop</h3>
	  <pre><code data-noescape class="perl">
 use Mojo::Base -strict;
 use Mojo::IOLoop;

 my $promise = Mojo::IOLoop->delay;
 Mojo::IOLoop->timer(2 => sub{ $promise->resolve(2) });

 $promise->then(sub{
     my $waited = shift;

     my ($t1, $t3) = map { Mojo::IOLoop->delay } 1..2;
     Mojo::IOLoop->timer(1 => sub{ $t1->resolve(1 + $waited) });
     Mojo::IOLoop->timer(3 => sub{ $t1->resolve(3 + $waited) });

     $t1->race($t3);

 })->then(sub{ say "Done after $_[0] seconds." })->wait;

    </code></pre>
	</section>

	<section>
	  <h1>+ Web<h1>
	</section>

	<section>
	  <pre><code data-noescape class="perl">
 use Mojolicious::Lite;

 my $hn = Mojo::URL->new('https://news.ycombinator.com/news');

 get '/' => sub {
     my $c = shift;

     $c->ua->get( $hn => sub{
         $c->render( json => pop->res->dom->find('a.storylink')->map(sub{
             [ $_->all_text, $_->attr('href') ]
         })->to_array );
     });
 };

 app->start;

    </code></pre>
	</section>

	<section>
	  <pre><code data-noescape class="perl">
 use Mojolicious::Lite;
 use Mojo::Collection qw(c);

 my $hn = Mojo::URL->new('https://news.ycombinator.com/news');

 get '/:pages' => { pages => 3 } => sub {
     my $c = shift;

     $c->render_later;
     Mojo::IOLoop->delay(
         sub {
             my $delay = shift;
             $c->ua->get($hn->clone->query(p=>$_) => $delay->begin)
                 for 1 .. $c->stash('pages');
         },

         sub {
             my $delay = shift;
             $c->render( json => c(@_)->map(sub{$_->res->dom->find('a.storylink')})
                 ->flatten->map(sub{
                     [ $_->all_text, $_->attr('href') ]
                 })->to_array
             )
         }
     );
 };

 app->start;

    </code></pre>
	</section>

	<section>
	  <pre><code data-noescape class="perl">
 use Mojolicious::Lite;
 use Mojo::Collection qw(c);

 my $hn = Mojo::URL->new('https://news.ycombinator.com/news');

 get '/:pages' => { pages => 3 } => sub {
     my $c = shift;

     my ($f, @rest) = map { $c->ua->get_p($hn->clone->query(p=>$_)) }
                          1 .. $c->stash('pages');

     $c->render_later;
     $f->all(@rest)->then(sub{
         $c->render( json => c(@_)->flatten->map(sub{$_->res->dom->find('a.storylink')})
             ->flatten->map(sub{
                 [ $_->all_text, $_->attr('href') ]
             })->to_array
         )
     })->catch(sub{
         $c->render( json => { error => [@_] } )
     });
 };

 app->start;

    </code></pre>
	</section>

	<section>
	  <pre><code data-noescape class="perl">
 use Mojolicious::Lite;

 my $proxy_url = Mojo::URL->new('https://metacpan.org/');

 any '/*rest' => { rest => '' } => sub {
     my $c = shift;

     my $tx = Mojo::Transaction::HTTP->new( req => $c->req->clone );
     $tx->req->url->$_($proxy_url->$_) for qw/ scheme host port /;
     $tx->req->headers->header( Host => $tx->req->url->host_port );

     $c->render_later;
     $c->ua->start($tx, sub {
          my ($ua,$tx) = @_;
          $c->tx->res($tx->res);
          $c->rendered;
     });
 };

 app->start;

    </code></pre>
	</section>

	<section>
	  <pre><code data-noescape class="perl">
 use Mojolicious::Lite;
 use Mojo::Cache;

 my $cache     = Mojo::Cache->new;
 my $proxy_url = Mojo::URL->new('https://metacpan.org/');

 any '/*rest' => { rest => '' } => sub {
     my $c   = shift;
     my $key = join '=', map {$c->req->$_} qw/ method url /;

     if ( my $res = $cache->get($key) ) {
         return $c->res($res) && $c->rendered;
     }

     my $tx = Mojo::Transaction::HTTP->new( req => $c->req->clone );
     $tx->req->url->$_($proxy_url->$_) for qw/ scheme host port /;
     $tx->req->headers->header( Host => $tx->req->url->host_port );
     $c->render_later;
     $c->ua->start($tx, sub {
          my ($ua,$tx) = @_;
          $cache->set($key => $tx->res);
          $c->tx->res($tx->res);
          $c->rendered;
     });
 };

 app->start;

    </code></pre>
	</section>

	<section>
	  <h1>...</h1>
	</section>

	<section data-background="yellow">
	<h1>Thanks!</h1>
	</section>
	<section data-background="red">
	  <img src="img/bender-applause.png" alt="" class="plain">
	</section>
	<section>
	<h1>Questions?</h1>
	</section>

	<section>
	  <pre><code data-trim data-noescape class="perl">
#!/usr/bin/env perl

use Mojo::Base --strict;
use Mojo::UserAgent;

my $ua = Mojo::UserAgent->new->inactivity_timeout(360)->request_timeout(360);

for my $plan ( get_plan(@ARGV) ) { Mojo::IOLoop->delay(@$plan)->wait }

sub get_plan {
    my $base = 'http://deploy:6200/';

    map {
        my @tasks = @$_; [

        sub{
            my $delay = shift;
            say '> '. pp(\@tasks);
            $ua->get($_ => { 'ss-from' => $ENV{USER} } => $delay->begin) for map {$base.$_} @tasks;
        },
        sub{
            my $delay = shift;
            my $count = 0;
            say '- '. $tasks[$count++].":\n". $_->res->body for @_;
            say '< '. pp(\@tasks) . "\n";
        }
    ]} map {[ split m|/\+/| ]} split( m|/\-/|, join('/', @_) )
}
	  </code></pre>
	</section>

      </div>
    </div>
    <script src="lib/js/head.min.js"></script>
    <script src="js/reveal.js"></script>
    <script>
      // Full list of configuration options available here:
      // https://github.com/hakimel/reveal.js#configuration
      Reveal.initialize({
        controls: false,
        progress: false,
        history: true,
        center: true,

        theme: Reveal.getQueryHash().theme, // available themes are in /css/theme
        transition: Reveal.getQueryHash().transition || 'none', // default/cube/page/concave/zoom/linear/fade/none

        // Parallax scrolling
        // parallaxBackgroundImage: 'https://s3.amazonaws.com/hakim-static/reveal-js/reveal-parallax-1.jpg',
        // parallaxBackgroundSize: '2100px 900px',

        // Optional libraries used to extend on reveal.js
        dependencies: [
            { src: 'lib/js/classList.js', condition: function() { return !document.body.classList; } },
            { src: 'plugin/highlight/highlight.js', async: true, callback: function() { hljs.initHighlightingOnLoad(); } },
            { src: 'plugin/zoom-js/zoom.js', async: true, condition: function() { return !!document.body.classList; } },
            { src: 'plugin/notes/notes.js', async: true, condition: function() { return !!document.body.classList; } }
        ]
      });
    </script>
  </body>
</html>
